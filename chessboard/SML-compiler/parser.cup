/* ----------------------Preliminary Declarations Section--------------------*/

/*import java_cup.runtime.*;
import music.*;
*/
package sml.compiler;

import sml.elements.*;
import sml.interfaces.*;


parser code {:

public void report_error(String message, Object info) {

    StringBuilder m = new StringBuilder("Error");
    if (info instanceof java_cup.runtime.Symbol) {
        java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
        if (s.left >= 0) {
            m.append(" in line "+(s.left+1));
            if (s.right >= 0)
                m.append(", column "+(s.right+1));
        }
    }
    m.append(" : "+message);
    System.err.println(m);
}

public void report_fatal_error(String message, Object info) {
    report_error(message, info);
    System.exit(1);
}

:};

/* ------------Declaration of Terminals and Non Terminals Section----------- */


terminal           SEMI, PLUS, MINUS,LPAREN, RPAREN,LACCOL, RACCOL, EQUAL, COMA;
terminal           LET, ENS, BEGIN, END, TEMPO, PLAY, SCALE, CHORD, SEQUENCE, REST, INSTRU, REPEAT, TRANSPOSE;
terminal String    LETTER, COMPLEXNOTE;
terminal Integer   NUMBER;
terminal String    STRING,SCALENAME;

non terminal Object     music, declarations,nedeclarations, body, declaration, scale, musical_element;
non terminal Object     instrument, note_sequence, complex_note, instructions, tempo, instruction;
non terminal Object     play, playable_element, note, playable_chord, playable_sequence;
non terminal Object     sequence, chord, time;
non terminal Object     identifiant, repeat, transpose;
non terminal Object     scale_type, alteration ;
non terminal Object     scale_definition, musical_element_definition, instrument_definition, time_definition;
non terminal Object     scale_redefinition, musical_element_list;
non terminal Object     musical_element_and_note_sequence;
non terminal Object     velocity;


/* -------------Precedence and Associatively of Terminals Section----------- */


/* ----------------------------Grammar Section-------------------- */

music ::= declarations:d body:b {: RESULT = new Music((Declarations)d,(Body)b); :}
;

declarations ::= {: RESULT = null ; :}
    | nedeclarations : n {: RESULT = n ; :}
;

nedeclarations ::=  LET identifiant:i EQUAL declaration:d SEMI {: Declarations env = new Declarations(new Declaration((Variable)i,(IDeclarable)d),null) ;
        RESULT = env ;:}
    | nedeclarations:ds LET identifiant:i EQUAL declaration:d SEMI {: Declarations env = new Declarations(new   Declaration((Variable)i,(IDeclarable)d),(Declarations)ds) ;
        RESULT = env ;:}
;

declaration ::= identifiant:i {: RESULT = i ; :}
    | LPAREN declaration:d RPAREN {: RESULT = d ; :}
    | scale_definition:s {: RESULT = s ; :}
    | scale_redefinition:s {: RESULT = s ; :}
    | musical_element_definition:m {: RESULT = m ; :}
    | instrument_definition:i {: RESULT = i ; :}
    | time_definition:t {: RESULT = t ; :}
    | note_sequence:s {: RESULT = s ; :}
;

instrument ::= identifiant:i {: RESULT = i ; :}
    | identifiant:i velocity:v {: RESULT = new InstrumentWithVelocity((IInstrument)i,(int)v); :}
    | LPAREN instrument:i RPAREN {: RESULT = i ; :}
    | instrument_definition:i {: RESULT = i ; :}
;

instrument_definition ::= ENS instrument:i1 instrument:i2 {: RESULT = new Ens((IInstrument)i1,(IInstrument)i2) ; :}
    | INSTRU STRING:s {: RESULT = Instrument.instrument(s); :}
    | INSTRU STRING:s velocity:v {: RESULT = new InstrumentWithVelocity(Instrument.instrument(s),(int)v); :}
;

musical_element_list ::= musical_element:e {: RESULT=e ; :}
    | musical_element:e COMA musical_element_list:n {: RESULT = new Sequence((IMusicalElement)e,(IMusicalElement)n) ; :}
    | note_sequence:s {: RESULT = s ; :}
    | note_sequence:s COMA musical_element_list:n {: RESULT = new Chord((IMusicalElement)s,(IMusicalElement)n) ; :}
;

note_sequence ::= LACCOL musical_element_list:m RACCOL {: RESULT = m; :}
;

body ::= BEGIN instructions:i END SEMI {: RESULT = i ; :}
;

instructions ::= {: RESULT = null ; :}
    | instruction:i SEMI instructions:is {: RESULT = new Body((IInstruction)i,(Body)is) ; :}
;

instruction ::= tempo:t {: RESULT = t ; :}
    | play:p {: RESULT = p ; :}
;

tempo ::= TEMPO NUMBER:n {: RESULT = new Tempo((int)n); :}
;

play ::= PLAY scale:s playable_element:p {: RESULT = new Play((IPlayableScale)s,(IPlayableElement)p) ; :}
;

scale ::= identifiant:i {: RESULT = i ; :}
    | LPAREN scale:s RPAREN {: RESULT = s ; :}
    | scale_definition:s  {: RESULT = s ; :}
;

scale_definition ::= SCALE note:n scale_type:s {: RESULT = new Scale((Note)n,(IScale)s) ; :}
;

scale_type ::=  LPAREN scale_type:s RPAREN {: RESULT = s ; :}
    | SCALENAME:s {: RESULT = ScaleName.scaleName(s); :}
    | identifiant:i {: RESULT=i ; :}
    | scale_redefinition:s {: RESULT = s; :}
;

scale_redefinition ::= SCALE scale_type:s LACCOL alteration:a1 COMA alteration:a2 COMA alteration:a3 COMA
                    alteration:a4 COMA alteration:a5 COMA alteration:a6 COMA alteration:a7 RACCOL
    {: RESULT = new AlterableScale((IScale)s,(Boolean)a1,(Boolean)a2,(Boolean)a3,(Boolean)a4,(Boolean)a5,(Boolean)a6,(Boolean)a7); :}
;

alteration ::= {: RESULT = null ; :}
    | PLUS {: RESULT = new Boolean(true); :}
    | MINUS {: RESULT = new Boolean(false); :}
;

note ::= note:n PLUS {: RESULT = ((Note)n).incr(1) ; :}
    | note:n MINUS {: RESULT = ((Note)n).decr(2) ; :}
    | LETTER:l {: RESULT = Note.note(l) ; :}
;

playable_element ::= instrument:i musical_element:m {: RESULT = new PlayableElement((IInstrument)i,(IMusicalElement)m) ; :}
    | LPAREN playable_element:p RPAREN {: RESULT = p ; :}
    | playable_chord:p {: RESULT = p ; :}
    | playable_sequence:p {: RESULT = p ; :}
;

playable_chord ::= CHORD playable_element:e1 playable_element:e2 {: RESULT = new PlayableChord((IPlayableElement)e1,(IPlayableElement)e2) ; :}
;

playable_sequence ::= SEQUENCE playable_element:e1 playable_element:e2 {: RESULT = new PlayableSequence((IPlayableElement)e1,(IPlayableElement)e2) ; :}
;

musical_element_and_note_sequence ::= identifiant:i {: RESULT = i ; :}
    | LPAREN musical_element:m RPAREN {: RESULT = m ; :}
    | musical_element_definition:m {: RESULT = m ; :}
    | note_sequence:s {: RESULT = s ; :}
;


musical_element ::= identifiant:i {: RESULT = i ; :}
    | LPAREN musical_element:m RPAREN {: RESULT = m ; :}
    | musical_element_definition:m {: RESULT = m ; :}
;

musical_element_definition ::= complex_note:c {: RESULT = c ; :}
    | sequence:s {: RESULT = s ; :}
    | chord:c {: RESULT = c ; :}
    | repeat:r {: RESULT = r ; :}
    | transpose:t {: RESULT = t ; :}
;

repeat ::= REPEAT NUMBER:n musical_element_and_note_sequence:m {: RESULT = new Repeat((int)n,(IMusicalElement)m); :}
;

transpose ::= TRANSPOSE NUMBER:n musical_element_and_note_sequence:m {: RESULT = new Transpose((int)n,(IMusicalElement)m); :}
    | TRANSPOSE MINUS NUMBER:n musical_element_and_note_sequence:m {: RESULT = new Transpose((int)-n,(IMusicalElement)m); :}
;

complex_note ::= COMPLEXNOTE:c {: RESULT = ComplexNote.parse(c) ; :}
    | REST time:t {: RESULT = new Rest((ITime)t) ; :}
;

time_definition ::= NUMBER:n {: RESULT = new Time((int)n) ; :}
;

time ::= identifiant:i {: RESULT = i ; :}
    | time_definition:t {: RESULT = t ; :}
;

chord ::= CHORD musical_element_and_note_sequence:e1 musical_element_and_note_sequence:e2 {: RESULT = new Chord((IMusicalElement)e1,(IMusicalElement)e2) ; :}
;

sequence ::= SEQUENCE musical_element_and_note_sequence:e1 musical_element_and_note_sequence:e2 {: RESULT = new Sequence((IMusicalElement)e1,(IMusicalElement)e2) ; :}
;

identifiant ::= STRING:s {: RESULT = new Variable((String)s); :}
    | LETTER:l {: RESULT = new Variable((String)l); :}
;

velocity ::= NUMBER:n {: RESULT = n ; :}
;


