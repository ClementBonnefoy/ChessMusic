SML
Grammaire Formelle


<music>		::= <declarations> <body>

<declarations>	::= ɛ
			| let [identifiant] = <declaration> ; <declarations>

<declaration> 		::= <scale>
			| <musical-element>
			| <instrument>
			| <time>
			| { <note-sequence> }

<instrument>		::= [identifiant]
			| ens <instrument> <instrument>
			| instr <id-instrument>

<note-sequence>	::= <complex-note>
			| <complex-note> , <note-sequence>			
			
<body>		::= begin <instructions> end ;

<instructions>		::= ɛ
			| <instruction> ; <instructions>

<instruction> 		::= <tempo>
			| <play>

<tempo> 		::= tempo [number]

<play> 		::= play <scale> <playable-element>

<scale> 		::= [identifiant]
			| scale <note> <scale-type>

<note> 		::= <note> +
			| <note> - 
			| a
			| b
			| c
			| d
			| e
			| f
			| g	
			
<instrument-id> ::= flute
			| piano

<scale-type> 		::= lydian
			| myxolydian
			| dorian
			| phrygian
			| aeolian
			| ionian
			| locrian

<playable-element>	::=<instrument> <musical-element>
			| <playable-chord>
			| <playable-sequence>

<playable-chord>	 ::= chord <playable-element> <playable-element>

<playable-sequence> ::= sequence <playable-element> <playable-element>			
		
<musical-element> 	::= [identifiant] 
			| <complex-note>
			| <sequence> 
			| <chord>
			
<complex-note> 	::= <role> <octave> <time>
			| <rest> <time>

<rest> ::= rest
		| ?
<role>			::= <role> +
			| <role> -
			| [number{1,2,3,4,5,6,7}]

<octave>		::= [char{a,b,c,d,e,f}]

<time> 		::= [number]
			| [identifiant]

<chord>		::= chord <musical-element> <musical-element>

<sequence> 		::= sequence <musical-element> <musical-element>